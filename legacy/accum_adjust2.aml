&args fac_grd num_inlets ingrds:REST
&type [date -full]
&echo &on

&if [null %fac_grd%] &then &do
  &call usage
  &return
&end 

&severity &error &routine Bailout

grid
disp 9999

&do i = 1 &to %num_inlets%
  &s grd%i% = [extract %i% [unquote %ingrds%]]
  &s i = %i% + 1
&end

&s start_path [show work]
&do i = 1 &to %num_inlets%
  &wo [dir [value grd%i%]]
  /*determine max FAC value
  &des [value grd%i%]
  &if [exists grd_t1 -grid] &then
    arc kill grd_t1
  setmask off
  setcell %grd$dx%
  setwindow [value grd%i%]
  grd_t1 = con([entryname [value grd%i%]] == %grd$zmax%,0)
  setmask grd_t1
  /*determine x,y of max FAC cell
  &if [exists pnt_t%i% -cover] &then
    arc kill pnt_t%i%
  pnt_t%i% = gridpoint(grd_t1,value)
  &des pnt_t%i%
  &s outlet_x%i% = %dsc$xmax%
  &s outlet_y%i% = %dsc$ymax%
  &s adjust_num%i%val = %grd$zmax%
  &type %i%, [value grd%i%], [value outlet_x%i%], [value outlet_y%i%]
  
  /*determine FDR value of max FAC cell
  &s fdr%i% = [show cellvalue [dir [value grd%i%]]/fdr [value outlet_x%i%], [value outlet_y%i%]]
  
  /*calc  x,y offset
  &select [value fdr%i%]
    &when 1
      &do
        &s adjust_num%i%x = [calc [value outlet_x%i%] + %grd$dx%]
        &s adjust_num%i%y = [value outlet_y%i%]
      &end
    &when 2
      &do
        &s adjust_num%i%x = [calc [value outlet_x%i%] + %grd$dx%]
        &s adjust_num%i%y = [calc [value outlet_y%i%] - %grd$dy%]
      &end
    &when 4
      &do
        &s adjust_num%i%x = [value outlet_x%i%]
        &s adjust_num%i%y = [calc [value outlet_y%i%] - %grd$dx%]
      &end
    &when 8
      &do
        &s adjust_num%i%x = [calc [value outlet_x%i%] - %grd$dx%]
        &s adjust_num%i%y = [calc [value outlet_y%i%] - %grd$dy%]
      &end
    &when 16
      &do
        &s adjust_num%i%x = [calc [value outlet_x%i%] - %grd$dx%]
        &s adjust_num%i%y = [value outlet_y%i%]
      &end
    &when 32
      &do
        &s adjust_num%i%x = [calc [value outlet_x%i%] - %grd$dx%]
        &s adjust_num%i%y = [calc [value outlet_y%i%] + %grd$dy%]
      &end
    &when 64
      &do
        &s adjust_num%i%x = [value outlet_x%i%]
        &s adjust_num%i%y = [calc [value outlet_y%i%] + %grd$dx%]
      &end
    &when 128
      &do
        &s adjust_num%i%x = [calc [value outlet_x%i%] + %grd$dx%]
        &s adjust_num%i%y = [calc [value outlet_y%i%] + %grd$dy%]
      &end
  &end
  &wo %start_path%
&end

&wo [dir %fac_grd%]
 setmask off
setwindow maxof
mape %fac_grd%
units map
&if [exists fac1 -grid] &then
  arc kill fac1
arc copy [entryname %fac_grd%] fac1

&do i = 1 &to %num_inlets%
  coord keyboard xy
  &if [exists pathgrd%i% -grid] &then
    arc kill pathgrd%i%
    pathgrd%i% = costpath(*,fil,fdr)
       1,[value adjust_num%i%x],[value adjust_num%i%y]
       3
       ~
       
  &if [exists fac_adj%i% -grid] &then
    arc kill fac_adj%i%
  fac_adj%i% = con(pathgrd%i%,fac%i% + [value adjust_num%i%val],fac%i%)
  &s val = %i% + 1
  &if [exists fac%val% -grid] &then
    arc kill fac%val%
  fac%val% = merge(fac_adj%i%,fac%i%)
&end


&if [exists fac_global.rrd -file] &then
  &s xx [delete fac_global.rrd -file]
&if [exists fac_global.aux -file] &then
  &s xx [delete fac_global.aux -file]

&if [exists fac_global -grid] &then
  arc kill fac_global

rename fac%val% fac_global


/*&if [exists str%thresh2% -grid] &then
/*  arc kill str%thresh2%

/*str%thresh2% = con ( fac_global ge %thresh2%, 1)
  
/*cleanup
&do i = 1 &to %num_inlets%
  &if [exists grd_t1 -grid] &then
    arc kill grd_t1
  &if [exists fac_adj%i% -grid] &then
    arc kill fac_adj%i% all
  &if [exists pathgrd%i% -grid] &then
    arc kill pathgrd%i% all
  &if [exists fac%i% -grid] &then
    arc kill fac%i%
&end 
  
coord mouse  

quit

&return

&ROUTINE BAILOUT
&severity &error &ignore
coord mouse
&wo %start_path%
&lv
&return &error Bailing out of Accum_Adjust2.aml

&routine usage
&type  
&type  USAGE: &r accum_adjust2 <fac_grd> <num_inlets> 
&type                <space separated list of full paths to upstream fac grids>
&type   
&type  ex: &r d:\sstoolbox\accum_adjust2 3 D:\streamstats\DataPrepWorkshop10\ex1\1111\fac D:\streamstats\DataPrepWorkshop10\ex1\2222\fac D:\streamstats\DataPrepWorkshop10\ex1\3333\fac 
&type 
&type This AML adjusts the fac of a downstream HUC to include flow accumulations from
&type upstream HUC's. Run this from the downstream HUC workspace. The AML will leave 
&type the fac grid intact and will create agrid named "fac_global". To get true 
&type accumulation values in HUCs downstream of other non-headwater HUCs, proceed 
&type from upstream HUCs to downstream HUCs in order, and specify the fac_global grid 
&type for any upstream HUC that has one. (It is not essential that the fac_global
&type contain true global fac values, and in some cases it is not possible since the 
&type values get too large. In practice, as long as the receiving cells have 
&type accumulation values larger than the stream definition threshold 
&type (150,000 cells for 10-m grids), then it will be OK. 
&type  
&return  /* end of usage routine